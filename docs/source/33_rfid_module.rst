.. note::

    ã“ã‚“ã«ã¡ã¯ï¼SunFounderã®Raspberry Pi & Arduino & ESP32æ„›å¥½è€…ã‚³ãƒŸãƒ¥ãƒ‹ãƒ†ã‚£ã¸ã‚ˆã†ã“ãï¼ã“ã®Facebookã‚°ãƒ«ãƒ¼ãƒ—ã§ã€Raspberry Piã€Arduinoã€ESP32ã®ä¸–ç•Œã‚’ã‚‚ã£ã¨æ·±ãæ¢æ±‚ã—ã€ä»–ã®æ„›å¥½è€…ãŸã¡ã¨äº¤æµã—ã¾ã—ã‚‡ã†ã€‚

    **å‚åŠ ã®ãƒ¡ãƒªãƒƒãƒˆ**

    - **å°‚é–€çš„ãªã‚µãƒãƒ¼ãƒˆ**: è³¼å…¥å¾Œã®å•é¡Œã‚„æŠ€è¡“çš„ãªèª²é¡Œã‚’ã€ã‚³ãƒŸãƒ¥ãƒ‹ãƒ†ã‚£ã‚„ç§ãŸã¡ã®ãƒãƒ¼ãƒ ãŒã‚µãƒãƒ¼ãƒˆã—ã¾ã™ã€‚
    - **å­¦ã³ã¨å…±æœ‰**: æŠ€è¡“ã‚’å‘ä¸Šã•ã›ã‚‹ãŸã‚ã®ãƒ’ãƒ³ãƒˆã‚„ãƒãƒ¥ãƒ¼ãƒˆãƒªã‚¢ãƒ«ã‚’äº¤æ›ã—ã¾ã—ã‚‡ã†ã€‚
    - **é™å®šãƒ—ãƒ¬ãƒ“ãƒ¥ãƒ¼**: æ–°è£½å“ã®ç™ºè¡¨ã‚„ãƒ—ãƒ¬ãƒ“ãƒ¥ãƒ¼ã‚’ã„ã¡æ—©ããƒã‚§ãƒƒã‚¯ã§ãã¾ã™ã€‚
    - **ç‰¹åˆ¥å‰²å¼•**: æœ€æ–°è£½å“ã‚’ç‰¹åˆ¥ä¾¡æ ¼ã§è³¼å…¥ã§ãã¾ã™ã€‚
    - **ã‚¤ãƒ™ãƒ³ãƒˆã‚„ãƒ—ãƒ¬ã‚¼ãƒ³ãƒˆä¼ç”»**: ã‚­ãƒ£ãƒ³ãƒšãƒ¼ãƒ³ã‚„ãƒ—ãƒ¬ã‚¼ãƒ³ãƒˆä¼ç”»ã«å‚åŠ ã§ãã¾ã™ã€‚

    ğŸ‘‰ ç§ãŸã¡ã¨ä¸€ç·’ã«æ–°ã—ã„å‰µé€ ã‚’å§‹ã‚ã¾ã—ã‚‡ã†ï¼[|link_sf_facebook|]ã‚’ã‚¯ãƒªãƒƒã‚¯ã—ã¦ã€ä»Šã™ãå‚åŠ ã—ã¦ãã ã•ã„ï¼

.. _ar_rfid_module:

33. RF522-RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®æ¢æ±‚
================================================
ãƒ¬ã‚¸ã§é•·æ™‚é–“å¾…ãŸã•ã‚Œã‚‹æ—¥ã€…ã¯ã€RFIDæŠ€è¡“ã®ãŠã‹ã’ã§éå»ã®ã‚‚ã®ã¨ãªã‚Šã¾ã—ãŸï¼RFIDã‚’ä½¿ç”¨ã—ãŸè‡ªå‹•ãƒã‚§ãƒƒã‚¯ã‚¢ã‚¦ãƒˆã‚·ã‚¹ãƒ†ãƒ ã‚’æƒ³åƒã—ã¦ãã ã•ã„ã€‚ã‚«ãƒ¼ãƒˆã«å•†å“ã‚’å…¥ã‚Œã€ãã®ã¾ã¾å‡ºå£ã¸é€²ã‚€ã ã‘ã§ã€RFIDã‚¿ã‚°ãŒä»˜ã„ãŸå„å•†å“ãŒç¬æ™‚ã«æ¤œå‡ºãƒ»è¨˜éŒ²ã•ã‚Œã¾ã™ã€‚ã‚‚ã†ä¸€ã¤ä¸€ã¤ã®å•†å“ã‚’ã‚¹ã‚­ãƒ£ãƒ³ã™ã‚‹å¿…è¦ã¯ã‚ã‚Šã¾ã›ã‚“ï¼

ã“ã®ã‚¨ã‚­ã‚µã‚¤ãƒ†ã‚£ãƒ³ã‚°ãªãƒ¬ãƒƒã‚¹ãƒ³ã§ã¯ã€RC522 RFIDãƒªãƒ¼ãƒ€ãƒ¼/ãƒ©ã‚¤ã‚¿ãƒ¼ãƒ»ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ã£ã¦RFIDæŠ€è¡“ã®ä¸–ç•Œã«é£›ã³è¾¼ã¿ã¾ã™ã€‚ã“ã®ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯ã€ä½æ¶ˆè²»é›»åŠ›ã€æ‰‹é ƒãªä¾¡æ ¼ã€è€ä¹…æ€§ã€ç°¡å˜ãªã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã€ãã—ã¦åºƒç¯„ãªæ™®åŠã«ã‚ˆã£ã¦ã€ãƒ›ãƒ“ã‚¤ã‚¹ãƒˆã®é–“ã§ç‰¹ã«äººæ°—ãŒã‚ã‚Šã¾ã™ã€‚

.. raw:: html

    <video muted controls style = "max-width:90%">
        <source src="_static/video/33_rfid_lcd.mp4" type="video/mp4">
        Your browser does not support the video tag.
    </video>

ã“ã®ãƒ¬ãƒƒã‚¹ãƒ³ã§å­¦ã¶ã“ã¨:

* RFIDæŠ€è¡“ã®åŸºç¤ã¨ãã®å¿œç”¨ã«ã¤ã„ã¦ç†è§£ã™ã‚‹
* RC522 RFIDãƒªãƒ¼ãƒ€ãƒ¼ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’Arduinoã¨ã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã™ã‚‹æ–¹æ³•ã‚’å­¦ã¶
* RC522ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ã£ã¦RFIDã‚¿ã‚°ã«å€‹äººãƒ‡ãƒ¼ã‚¿ã‚’æ›¸ãè¾¼ã‚€
* RFIDã‚¿ã‚°ã‹ã‚‰æ›¸ãè¾¼ã‚“ã ãƒ‡ãƒ¼ã‚¿ã‚’èª­ã¿å–ã‚Šã€ç¢ºèªã™ã‚‹
* RFIDã‚¿ã‚°æƒ…å ±ã‚’I2C LCDã«è¡¨ç¤ºã™ã‚‹

RC522ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨RFIDæŠ€è¡“ã¨ã¯ï¼Ÿ
-------------------------------------------------
**RC522ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«**

RC522 RFIDãƒªãƒ¼ãƒ€ãƒ¼ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯ã€13.56MHzã®å‘¨æ³¢æ•°ã§å‹•ä½œã—ã€ISO 14443Aè¦æ ¼ã«æº–æ‹ ã—ãŸRFIDã‚¿ã‚°ã¨é€šä¿¡ã™ã‚‹ã‚ˆã†ã«è¨­è¨ˆã•ã‚Œã¦ã„ã¾ã™ã€‚ã“ã®ã‚³ãƒ³ãƒ‘ã‚¯ãƒˆã§å¤šç”¨é€”ãªãƒ‡ãƒã‚¤ã‚¹ã¯ã€ã‚¢ã‚¯ã‚»ã‚¹åˆ¶å¾¡ã€åœ¨åº«è¿½è·¡ã€éæ¥è§¦æ±ºæ¸ˆã‚·ã‚¹ãƒ†ãƒ ãªã©ã®ç”¨é€”ã«æœ€é©ã§ã€4ãƒ”ãƒ³ã®SPIæ¥ç¶šã‚’ä»‹ã—ã¦ãƒã‚¤ã‚¯ãƒ­ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ã¨ã‚¤ãƒ³ã‚¿ãƒ¼ãƒ•ã‚§ãƒ¼ã‚¹ã—ã€æœ€å¤§10Mbpsã®ãƒ‡ãƒ¼ã‚¿ãƒ¬ãƒ¼ãƒˆã‚’ã‚µãƒãƒ¼ãƒˆã—ã¾ã™ã€‚ã•ã‚‰ã«ã€I2CãŠã‚ˆã³UARTé€šä¿¡ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã‚‚ã‚µãƒãƒ¼ãƒˆã—ã¦ã„ã¾ã™ã€‚

2.5ã€œ3.3Vã§å‹•ä½œã—ã€5Vè€æ€§ã®ã‚ã‚‹ãƒ­ã‚¸ãƒƒã‚¯ãƒ”ãƒ³ã‚’æŒã¤ãŸã‚ã€Arduinoã‚„ãã®ä»–ã®5Vãƒ­ã‚¸ãƒƒã‚¯ãƒã‚¤ã‚¯ãƒ­ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ã¨ã®çµ±åˆãŒå®¹æ˜“ã§ã€ãƒ­ã‚¸ãƒƒã‚¯ãƒ¬ãƒ™ãƒ«ã‚³ãƒ³ãƒãƒ¼ã‚¿ãŒä¸è¦ã§ã™ã€‚

.. image:: img/33_rfid_module.png
  :width: 400
  :align: center

* **VCC**: ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã«é›»æºã‚’ä¾›çµ¦ã—ã¾ã™ï¼ˆ2.5ã€œ3.3Vï¼‰ã€‚Arduinoã®3.3Vå‡ºåŠ›ã«æ¥ç¶šã—ã¾ã™ã€‚5Vã«æ¥ç¶šã—ãªã„ã§ãã ã•ã„ã€‚ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ãŒæ•…éšœã™ã‚‹å¯èƒ½æ€§ãŒã‚ã‚Šã¾ã™ã€‚
* **RST**: ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ãƒªã‚»ãƒƒãƒˆã—ã€é›»æºã‚’ã‚ªãƒ•ã«ã—ã¾ã™ã€‚ä½é›»åœ§ã§ãƒ‘ãƒ¯ãƒ¼ãƒ€ã‚¦ãƒ³ãƒ¢ãƒ¼ãƒ‰ã«å…¥ã‚Šã€ç«‹ã¡ä¸ŠãŒã‚Šã‚¨ãƒƒã‚¸ã§ãƒªã‚»ãƒƒãƒˆã•ã‚Œã¾ã™ã€‚
* **GND**: ã‚°ãƒ©ãƒ³ãƒ‰ãƒ”ãƒ³ã§ã€Arduinoã®GNDã«æ¥ç¶šã—ã¾ã™ã€‚
* **IRQ**: å‰²ã‚Šè¾¼ã¿ãƒ”ãƒ³ã§ã€RFIDã‚¿ã‚°ãŒè¿‘ãã«ã‚ã‚‹ã¨ãã«ãƒã‚¤ã‚¯ãƒ­ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ã«é€šçŸ¥ã—ã¾ã™ã€‚
* **MISO / SCL / Tx**: SPIã®ãƒã‚¹ã‚¿ãƒ¼å…¥åŠ›ã‚¹ãƒ¬ãƒ¼ãƒ–å‡ºåŠ›ã€I2Cã®ã‚·ãƒªã‚¢ãƒ«ã‚¯ãƒ­ãƒƒã‚¯ã€ãŠã‚ˆã³UARTã®ã‚·ãƒªã‚¢ãƒ«ãƒ‡ãƒ¼ã‚¿å‡ºåŠ›ã¨ã—ã¦æ©Ÿèƒ½ã—ã¾ã™ã€‚
* **MOSI**: ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¸ã®SPIå…¥åŠ›ã€‚
* **SCK**: SPIãƒã‚¹ãƒã‚¹ã‚¿ãƒ¼ï¼ˆArduinoï¼‰ã‹ã‚‰ã®ã‚·ãƒªã‚¢ãƒ«ã‚¯ãƒ­ãƒƒã‚¯å…¥åŠ›ã€‚
* **SS / SDA / Rx**: SPIã®ä¿¡å·å…¥åŠ›ã€I2Cã®ã‚·ãƒªã‚¢ãƒ«ãƒ‡ãƒ¼ã‚¿ã€ãŠã‚ˆã³UARTã®ã‚·ãƒªã‚¢ãƒ«ãƒ‡ãƒ¼ã‚¿å…¥åŠ›ã€‚å››è§’ã§ãƒãƒ¼ã‚¯ã•ã‚Œã‚‹ã“ã¨ãŒå¤šã„ã§ã™ã€‚

**RFIDæŠ€è¡“**

RFIDã‚·ã‚¹ãƒ†ãƒ ã€ã¤ã¾ã‚Šç„¡ç·šå‘¨æ³¢æ•°è­˜åˆ¥ã‚·ã‚¹ãƒ†ãƒ ã¯ã€è­˜åˆ¥ã™ã‚‹ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã«å–ã‚Šä»˜ã‘ã‚‰ã‚ŒãŸã‚¿ã‚°ã¨ã€ã“ã®ã‚¿ã‚°ã‚’èª­ã¿å–ã‚‹ãƒªãƒ¼ãƒ€ãƒ¼ã¨ã„ã†2ã¤ã®ä¸»è¦ãªã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆã§æ§‹æˆã•ã‚Œã¦ã„ã¾ã™ã€‚

ãƒªãƒ¼ãƒ€ãƒ¼ã«ã¯RFãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨ã‚¢ãƒ³ãƒ†ãƒŠãŒå«ã¾ã‚Œã¦ãŠã‚Šã€é«˜å‘¨æ³¢ã®é›»ç£å ´ã‚’ç”Ÿæˆã—ã¾ã™ã€‚ã‚¿ã‚°ã¯é€šå¸¸ãƒ‘ãƒƒã‚·ãƒ–ã§ã€ãƒãƒƒãƒ†ãƒªãƒ¼ã‚’æŒãŸãšã€æƒ…å ±ã®ä¿å­˜ã¨å‡¦ç†ã‚’è¡Œã†ãƒã‚¤ã‚¯ãƒ­ãƒãƒƒãƒ—ã¨ã€ä¿¡å·ã‚’å—ä¿¡ãƒ»é€ä¿¡ã™ã‚‹ã‚¢ãƒ³ãƒ†ãƒŠã§æ§‹æˆã•ã‚Œã¦ã„ã¾ã™ã€‚

.. image:: img/33_rfid_com.png
  :width: 800
  :align: center

ã‚¿ã‚°ãŒãƒªãƒ¼ãƒ€ãƒ¼ã«è¿‘ã¥ãã¨ã€ãƒªãƒ¼ãƒ€ãƒ¼ã®é›»ç£å ´ãŒã‚¿ã‚°ã®ãƒãƒƒãƒ—ã«é›»åŠ›ã‚’ä¾›çµ¦ã—ã€ã‚¢ãƒ³ãƒ†ãƒŠã‚’é€šã˜ã¦é›»å­ã®æµã‚Œã‚’èª˜ç™ºã—ã¾ã™ã€‚

ãã®å¾Œã€ãƒãƒƒãƒ—ã¯ä¿å­˜ã•ã‚ŒãŸãƒ‡ãƒ¼ã‚¿ã‚’ç„¡ç·šä¿¡å·ã¨ã—ã¦ãƒªãƒ¼ãƒ€ãƒ¼ã«é€ã‚Šè¿”ã—ã¾ã™ã€‚ã“ã®ãƒ—ãƒ­ã‚»ã‚¹ã¯ã€Œãƒãƒƒã‚¯ã‚­ãƒ£ãƒƒã‚¿ãƒªãƒ³ã‚°ã€ã¨ã—ã¦çŸ¥ã‚‰ã‚Œã¦ã„ã¾ã™ã€‚ãƒªãƒ¼ãƒ€ãƒ¼ã¯ã“ã®ä¿¡å·ã‚’ã‚­ãƒ£ãƒ—ãƒãƒ£ã—ã€ãƒ‡ã‚³ãƒ¼ãƒ‰ã—ã¦æƒ…å ±ã‚’ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ã‚¿ã‚„ãƒã‚¤ã‚¯ãƒ­ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ã«é€ä¿¡ã—ã€ã•ã‚‰ãªã‚‹å‡¦ç†ã‚’è¡Œã„ã¾ã™ã€‚

å›è·¯ã®æ§‹ç¯‰
------------------------------------
ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã«ã¤ã„ã¦ã™ã¹ã¦ç†è§£ã—ãŸã¨ã“ã‚ã§ã€æ¬¡ã¯Arduinoã«æ¥ç¶šã—ã¦ã¿ã¾ã—ã‚‡ã†ï¼

**å¿…è¦ãªã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆ**

.. list-table:: 
   :widths: 25 25 25
   :header-rows: 0

   * - 1 * Arduino Uno R3
     - 1 * RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨ã‚¿ã‚°
     - 1 * I2C LCD1602
   * - |list_uno_r3|
     - |list_rc522_module| 
     - |list_i2c_lcd1602|
   * - ã‚¸ãƒ£ãƒ³ãƒ‘ãƒ¼ãƒ¯ã‚¤ãƒ¤ãƒ¼
     - 1 * ãƒ–ãƒ¬ãƒƒãƒ‰ãƒœãƒ¼ãƒ‰
     - 1 * USBã‚±ãƒ¼ãƒ–ãƒ«
   * - |list_wire|
     - |list_breadboard|
     - |list_usb_cable|

**æ§‹ç¯‰æ‰‹é †**

é…ç·šå›³ã«å¾“ã†ã‹ã€ä»¥ä¸‹ã®æ‰‹é †ã«å¾“ã£ã¦å›è·¯ã‚’æ§‹ç¯‰ã—ã¾ã™ã€‚

.. image:: img/33_rfid_connect_lcd.png
    :width: 700
    :align: center

1. ã¾ãšã€RC522-RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ãƒ–ãƒ¬ãƒƒãƒ‰ãƒœãƒ¼ãƒ‰ã«æŒ¿å…¥ã—ã¾ã™ã€‚

.. image:: img/33_rfid_plug_rc522.png
    :width: 400
    :align: center

2. æ¬¡ã«ã€RC522-RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨Arduino Uno R3ã‚’æ¥ç¶šã—ã¾ã™ã€‚

.. list-table::
    :widths: 20 20
    :header-rows: 1

    *   - RC522-RFID
        - Arduino UNO R3
    *   - 3.3V
        - 3.3V
    *   - RST
        - 9
    *   - GND
        - GND
    *   - IRQ
        -
    *   - MISO
        - 12
    *   - MOSI
        - 11
    *   - SCK
        - 13
    *   - SDA
        - 10
  
.. image:: img/33_rfid_connect_rc522.png
    :width: 500
    :align: center

3. æœ€å¾Œã«ã€I2C LCD1602ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’æ¥ç¶šã—ã¾ã™ã€‚GNDã‚’Arduino Uno R3ã®GNDã«ã€VCCã‚’5Vãƒ”ãƒ³ã«ã€SDAã‚’A4ãƒ”ãƒ³ã«ã€SCLã‚’A5ãƒ”ãƒ³ã«æ¥ç¶šã—ã¾ã™ã€‚

.. image:: img/33_rfid_connect_lcd.png
    :width: 700
    :align: center

ã‚³ãƒ¼ãƒ‰ä½œæˆ - æ›¸ãè¾¼ã¿ã¨èª­ã¿å–ã‚Š
---------------------------------------
ã“ã®ã‚»ã‚¯ã‚·ãƒ§ãƒ³ã§ã¯ã€MFRC522 RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã™ã‚‹ãŸã‚ã«å¿…è¦ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã€ã‚¿ã‚°ã«æƒ…å ±ã‚’æ›¸ãè¾¼ã¿ã€ã‚¿ã‚°ã‹ã‚‰æƒ…å ±ã‚’èª­ã¿å–ã‚‹ãŸã‚ã®ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã‚’é–‹ãã¾ã™ã€‚

**æƒ…å ±ã®æ›¸ãè¾¼ã¿**

1. MFRC522 RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã™ã‚‹ãŸã‚ã«ã¯ã€é©åˆ‡ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚ã¾ãšã€ **ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãƒãƒãƒ¼ã‚¸ãƒ£ãƒ¼** ã§ ``MFRC522`` ã‚’æ¤œç´¢ã—ã€ **ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«** ã‚’ã‚¯ãƒªãƒƒã‚¯ã—ã¾ã™ã€‚

.. image:: img/33_rfid_install_lib.png
  :align: center

2. æ¬¡ã«ã€ **ãƒ•ã‚¡ã‚¤ãƒ«** -> **ã‚µãƒ³ãƒ—ãƒ«** -> **MFRC522** ã‚’ã‚¯ãƒªãƒƒã‚¯ã™ã‚‹ã¨ã€ã•ã¾ã–ã¾ãªæ©Ÿèƒ½ã‚’ç¤ºã™ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ãŒè¡¨ç¤ºã•ã‚Œã¾ã™ã€‚ ``rfid_write_personal_data`` ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã‚’é–‹ãã¾ã™ã€‚

.. image:: img/33_rfid_open_write.png
  :align: center

3. **ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰** ã‚’ã‚¯ãƒªãƒƒã‚¯ã—ã¦ã€ã‚³ãƒ¼ãƒ‰ã‚’Arduinoãƒœãƒ¼ãƒ‰ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã—ã¾ã™ã€‚ãã®å¾Œã€ã‚·ãƒªã‚¢ãƒ«ãƒ¢ãƒ‹ã‚¿ã‚’é–‹ãã¨ã€ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒè¡¨ç¤ºã•ã‚Œã¾ã™ã€‚

.. image:: img/33_rfid_write_open.png
  :align: center

4. æ¬¡ã«ã€æä¾›ã•ã‚ŒãŸç™½ã„ã‚«ãƒ¼ãƒ‰ã‚„ã‚¿ã‚°ã‚’MFRC522ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®è¿‘ãã«ç½®ãã¾ã™ã€‚ã‚¿ã‚°ã®UIDã€PICCã‚¿ã‚¤ãƒ—ã€ãƒ•ã‚¡ãƒŸãƒªãƒ¼ãƒãƒ¼ãƒ ã®å…¥åŠ›ã‚’æ±‚ã‚ã‚‹ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒè¡¨ç¤ºã•ã‚Œã¾ã™ã€‚æœ€å¾Œã«ã€Œ#ã€ã‚’å…¥åŠ›ã—ã¾ã™ã€‚

.. code-block::

  Write personal data on a MIFARE PICC 
  Card UID: 9B 2F 0A 11 PICC type: MIFARE 1KB
  Type Family name, ending with #

5. æ¬¡ã«å…¥åŠ›ã‚’é–‹å§‹ã—ã¾ã™ã€‚ä¾‹ãˆã°ã€ ``XIE#`` ã¨å…¥åŠ›ã—ã¾ã™ã€‚ ``Enter`` ã‚­ãƒ¼ã‚’æŠ¼ã—ã¦å…¥åŠ›ã‚’Arduinoãƒœãƒ¼ãƒ‰ã«é€ä¿¡ã™ã‚‹ã¨ã€ãã‚ŒãŒRFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã«è»¢é€ã•ã‚Œã¾ã™ã€‚

.. note::

  ãƒ‡ãƒ¼ã‚¿ã‚’å…¥åŠ›ã™ã‚‹éš›ã¯ã€ã‚«ãƒ¼ãƒ‰ã‚„ã‚¿ã‚°ã‚’RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ã‚¢ãƒ³ãƒ†ãƒŠã«è¿‘ã¥ã‘ãŸã¾ã¾ã«ã—ã¦ãŠã„ã¦ãã ã•ã„ã€‚ãã†ã—ãªã„ã¨ã‚¨ãƒ©ãƒ¼ãŒç™ºç”Ÿã—ã¾ã™ã€‚

.. image:: img/33_rfid_write_first_name.png
  :align: center

6. ãƒ‡ãƒ¼ã‚¿ã®æ›¸ãè¾¼ã¿ãŒæˆåŠŸã—ãŸãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒè¡¨ç¤ºã•ã‚Œã€ãã®å¾Œã«ãƒ•ã‚¡ãƒ¼ã‚¹ãƒˆãƒãƒ¼ãƒ ã®å…¥åŠ›ã‚’æ±‚ã‚ã‚‰ã‚Œã¾ã™ã€‚

.. code-block::

  Write personal data on a MIFARE PICC 
  Card UID: 9B 2F 0A 11 PICC type: MIFARE 1KB
  Type Family name, ending with #
  PCD_Authenticate() success: 
  MIFARE_Write() success: 
  MIFARE_Write() success: 
  Type First name, ending with #

7. æ¬¡ã«ã€ãƒ•ã‚¡ãƒ¼ã‚¹ãƒˆãƒãƒ¼ãƒ ã‚’å…¥åŠ›ã—ã¾ã™ã€‚ä¾‹ãˆã°ã€ ``Daisy#`` ã¨å…¥åŠ›ã—ã¾ã™ã€‚ãƒ‡ãƒ¼ã‚¿ã®æ›¸ãè¾¼ã¿ãŒæˆåŠŸã—ãŸãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒå†åº¦è¡¨ç¤ºã•ã‚Œã¾ã™ã€‚

.. code-block::

  Write personal data on a MIFARE PICC 
  Card UID: 9B 2F 0A 11 PICC type: MIFARE 1KB
  Type Family name, ending with #
  PCD_Authenticate() success: 
  MIFARE_Write() success: 
  MIFARE_Write() success: 
  Type First name, ending with #
  MIFARE_Write() success: 
  MIFARE_Write() success: 

**æƒ…å ±ã®èª­ã¿å–ã‚Š**

å…ˆã»ã©ã€ã‚«ãƒ¼ãƒ‰ã¾ãŸã¯ã‚¿ã‚°ã«åå‰ã‚’æ›¸ãè¾¼ã¿ã¾ã—ãŸã€‚æ¬¡ã«ã€åˆ¥ã®ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã‚’é–‹ãã€ã“ã®ã‚«ãƒ¼ãƒ‰ã‹ã‚‰æƒ…å ±ã‚’èª­ã¿å–ã‚Šã€ãƒ‡ãƒ¼ã‚¿ãŒæ­£ã—ãæ›¸ãè¾¼ã¾ã‚ŒãŸã‹ã‚’ç¢ºèªã—ã¾ã—ã‚‡ã†ã€‚

1. åŒæ§˜ã«ã€ **ãƒ•ã‚¡ã‚¤ãƒ«** -> **ã‚µãƒ³ãƒ—ãƒ«** -> **MFRC522** ã‚’ã‚¯ãƒªãƒƒã‚¯ã—ã€ ``rfid_read_personal_data`` ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã‚’é–‹ãã¾ã™ã€‚

.. image:: img/33_rfid_read_open.png
  :align: center

2. ã‚³ãƒ¼ãƒ‰ã‚’é–‹ã„ãŸã‚‰ã€Arduinoãƒœãƒ¼ãƒ‰ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã—ã¾ã™ã€‚ãã®å¾Œã€ã‚«ãƒ¼ãƒ‰ã‚’RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ã‚¢ãƒ³ãƒ†ãƒŠã®è¿‘ãã«ç½®ãã¾ã™ã€‚UIDã¨å…ˆã»ã©æ›¸ãè¾¼ã‚“ã åå‰ã®æƒ…å ±ãŒè¡¨ç¤ºã•ã‚Œã¾ã™ã€‚

.. code-block::

  **Card Detected:**
  Card UID: 9B 2F 0A 11
  Card SAK: 08
  PICC type: MIFARE 1KB
  Name: 
  Daisy XIE             
  **End Reading**

ã‚³ãƒ¼ãƒ‰ä½œæˆ - LCDã¸ã®è¡¨ç¤º
---------------------------------------

ã“ã“ã§ã¯ã€ã‚«ãƒ¼ãƒ‰ã®åå‰ã¨UIDã‚’I2C LCDã«è¡¨ç¤ºã™ã‚‹æ–¹æ³•ã‚’å­¦ã³ã¾ã™ã€‚

.. note::

  I2C LCD1602ã«ä¸æ…£ã‚Œãªå ´åˆã¯ã€ä»¥ä¸‹ã®ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã§åŸºæœ¬çš„ãªä½¿ã„æ–¹ã‚’å­¦ã¶ã“ã¨ãŒã§ãã¾ã™ã€‚

  * :ref:`ar_i2c_lcd1602`

  ``LiquidCrystal I2C`` ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ä½¿ç”¨ã—ã¾ã™ã€‚ã“ã®ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã¯ã€ **ãƒ©ã‚¤ãƒ–ãƒ©ãƒªãƒãƒãƒ¼ã‚¸ãƒ£ãƒ¼** ã‹ã‚‰ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã§ãã¾ã™ã€‚

1. Arduino IDEã‚’é–‹ãã€ **ãƒ•ã‚¡ã‚¤ãƒ«** ãƒ¡ãƒ‹ãƒ¥ãƒ¼ã‹ã‚‰ã€Œæ–°è¦ã‚¹ã‚±ãƒƒãƒã€ã‚’é¸æŠã—ã¦æ–°ã—ã„ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã‚’é–‹å§‹ã—ã¾ã™ã€‚
2. ã‚¹ã‚±ãƒƒãƒã‚’ ``Lesson33_RFID_LCD`` ã¨ã—ã¦ä¿å­˜ã—ã€ ``Ctrl + S`` ã‚’æŠ¼ã™ã‹ã€Œä¿å­˜ã€ã‚’ã‚¯ãƒªãƒƒã‚¯ã—ã¾ã™ã€‚

3. RFIDãŠã‚ˆã³LCDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨é€šä¿¡ã™ã‚‹ãŸã‚ã«ã€SPIãŠã‚ˆã³I2Cé€šä¿¡ç”¨ã®ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ã—ã¾ã™ã€‚RFIDãƒªãƒ¼ãƒ€ãƒ¼ã®ãƒªã‚»ãƒƒãƒˆãƒ”ãƒ³ï¼ˆ ``RST_PIN`` ï¼‰ã¨ã‚¹ãƒ¬ãƒ¼ãƒ–ã‚»ãƒ¬ã‚¯ãƒˆãƒ”ãƒ³ï¼ˆ ``SS_PIN`` ï¼‰ã‚’å®šç¾©ã—ã¾ã™ã€‚

.. code-block:: Arduino

  #include <SPI.h>                // Include the SPI library for SPI communication
  #include <MFRC522.h>            // Include the library for the RFID module
  #include <Wire.h>               // Include the Wire library for I2C communication
  #include <LiquidCrystal_I2C.h>  // Include the library for the I2C LCD

  #define RST_PIN 9  // Reset pin for the RFID module
  #define SS_PIN 10  // Slave select pin for the RFID module

4. RFIDãƒªãƒ¼ãƒ€ãƒ¼ã¨LCDãƒ‡ã‚£ã‚¹ãƒ—ãƒ¬ã‚¤ã‚’ã€æŒ‡å®šã•ã‚ŒãŸãƒ”ãƒ³æ¥ç¶šã¨LCDã®è¨­å®šï¼ˆã‚¢ãƒ‰ãƒ¬ã‚¹0x27ã€16åˆ—ã€2è¡Œï¼‰ã§åˆæœŸåŒ–ã—ã¾ã™ã€‚

.. code-block:: Arduino

  // Create an instance of the MFRC522 class to interface with the RFID module
  MFRC522 mfrc522(SS_PIN, RST_PIN);
  // Create an instance of the LiquidCrystal_I2C class for the LCD
  LiquidCrystal_I2C lcd(0x27, 16, 2);

5. ``setup()`` é–¢æ•°ã§ã¯ã€ã‚·ãƒªã‚¢ãƒ«é€šä¿¡ã€SPIãƒã‚¹ã€RFIDãƒªãƒ¼ãƒ€ãƒ¼ã€ãŠã‚ˆã³LCDã‚’åˆæœŸåŒ–ã—ã¾ã™ã€‚LCDã®ãƒãƒƒã‚¯ãƒ©ã‚¤ãƒˆã‚’è¨­å®šã—ã€ã‚·ãƒªã‚¢ãƒ«ãƒ¢ãƒ‹ã‚¿ã«æº–å‚™å®Œäº†ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’é€ä¿¡ã—ã¾ã™ã€‚

.. code-block:: Arduino

  void setup() {
    Serial.begin(9600);                         // Start serial communication at 9600bps
    SPI.begin();                                // Initialize the SPI bus
    mfrc522.PCD_Init();                         // Initialize the RFID reader
    lcd.init();                                 // Initialize the LCD display
    lcd.backlight();                            // Turn on the backlight of the LCD
    Serial.println(F("Ready to read a card"));  // Print a message to start read
  }

6. ``loop()`` é–¢æ•°ã¯ã€å¸¸ã«æ–°ã—ã„RFIDã‚«ãƒ¼ãƒ‰ãŒæ¤œå‡ºã•ã‚ŒãŸã‹ã‚’ãƒã‚§ãƒƒã‚¯ã—ã¾ã™ã€‚ã‚«ãƒ¼ãƒ‰ãŒæ¤œå‡ºã•ã‚Œã‚‹ã¨ã€UIDã‚’èª­ã¿å–ã‚Šè¡¨ç¤ºã—ã€ãƒ–ãƒ­ãƒƒã‚¯4ã‹ã‚‰ãƒ‡ãƒ¼ã‚¿ã‚’èª­ã¿å–ã‚Šè¡¨ç¤ºã—ã€å°‘ã—ã®é–“ãƒãƒ¼ã‚ºã‚’ç½®ã„ã¦ã‹ã‚‰LCDã‚’ã‚¯ãƒªã‚¢ã—ã¾ã™ã€‚

.. code-block:: Arduino

  void loop() {
    // Check if a new RFID card is present and can be read
    if (!mfrc522.PICC_IsNewCardPresent() || !mfrc522.PICC_ReadCardSerial()) {
      return;  // If no new card is present, exit the loop
    }

    displayCardUID();           // Function to display the UID of the card
    readAndDisplayBlock(4);     // Function to read and display block4 of the RFID card
    mfrc522.PICC_HaltA();       // Halt the RFID card to stop reading
    mfrc522.PCD_StopCrypto1();  // Stop encryption on the communication
    delay(5000);                // Delay for 5 seconds
    lcd.clear();                // Clear the LCD display
  }

7. ``displayCardUID()`` é–¢æ•°: ã‚«ãƒ¼ãƒ‰ã®UIDã‚’ã‚·ãƒªã‚¢ãƒ«ãƒ¢ãƒ‹ã‚¿ã¨LCDã®ä¸¡æ–¹ã«è¡¨ç¤ºã™ã‚‹ãŸã‚ã®é–¢æ•°ã§ã™ã€‚UIDã‚’16é€²æ•°å½¢å¼ã§è¡¨ç¤ºã—ã¾ã™ã€‚

.. code-block:: Arduino

  // Function to display the UID of the RFID card
  void displayCardUID() {
    Serial.print(F("Card UID:"));                  // Print the text "Card UID:"
    lcd.clear();                                   // Clear the LCD display
    lcd.setCursor(0, 0);                           // Set the LCD cursor to the top-left
    lcd.print("UID:");                             // Print "UID:" on the LCD
    for (byte i = 0; i < mfrc522.uid.size; i++) {  // Loop through each byte of the UID
      Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
      Serial.print(mfrc522.uid.uidByte[i], HEX);  // Print UID byte in hexadecimal
      lcd.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
      lcd.print(mfrc522.uid.uidByte[i], HEX);  // Print UID byte on the LCD in hexadecimal
    }
    Serial.println();  // Print a newline on the serial monitor
  }

8. ``authenticateBlock`` é–¢æ•°: RFIDã‚«ãƒ¼ãƒ‰ã‹ã‚‰ç‰¹å®šã®ãƒ–ãƒ­ãƒƒã‚¯ã‚’èª­ã¿å–ã‚Šã€ãã®ãƒ–ãƒ­ãƒƒã‚¯ã¸ã®ã‚¢ã‚¯ã‚»ã‚¹ã‚’èªè¨¼ã—ã€å–å¾—ã—ãŸãƒ‡ãƒ¼ã‚¿ã‚’LCDã«è¡¨ç¤ºã™ã‚‹é–¢æ•°ã§ã™ã€‚

.. code-block:: Arduino

  // Function to authenticate and read a block from the RFID card
  bool authenticateBlock(byte block, byte *buffer, byte &size) {
    MFRC522::StatusCode status;  // Variable to hold the status of RFID operations
    MFRC522::MIFARE_Key key;     // Variable to hold the authentication key
    // Set the key to the default key known as FFFFFFFFFFFFh
    for (byte i = 0; i < 6; i++) key.keyByte[i] = 0xFF;  // Default key A for authentication

    // Authenticate the desired block with the key
    status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, block, &key, &(mfrc522.uid));
    if (status != MFRC522::STATUS_OK) {
      Serial.print(F("Authentication failed: "));
      Serial.println(mfrc522.GetStatusCodeName(status));
      return false;  // If authentication failed, return false
    }

    // Read the block after successful authentication
    status = mfrc522.MIFARE_Read(block, buffer, &size);
    if (status != MFRC522::STATUS_OK) {
      Serial.print(F("Reading failed: "));
      Serial.println(mfrc522.GetStatusCodeName(status));
      return false;  // If reading failed, return false
    }
    buffer[size - 1] = '\0';  // Ensure the string is null-terminated
    return true;              // Return true if reading is successful
  }

9. ``readAndDisplayBlock`` é–¢æ•°: ã“ã®é–¢æ•°ã¯ã€äº‹å‰ã«å®šç¾©ã•ã‚ŒãŸã‚­ãƒ¼ã‚’ä½¿ç”¨ã—ã¦RFIDã‚«ãƒ¼ãƒ‰ä¸Šã®ç‰¹å®šã®ãƒ–ãƒ­ãƒƒã‚¯ã‚’èªè¨¼ã—ã€èªè¨¼ãŒæˆåŠŸã—ãŸå ´åˆã«ãƒ‡ãƒ¼ã‚¿ã‚’èª­ã¿å–ã‚Šã¾ã™ã€‚

.. code-block:: Arduino

  // Function to read a block and display its contents
  void readAndDisplayBlock(byte block) {
    byte buffer[18];                               // Buffer to store the data read from the RFID card
    byte size = sizeof(buffer);                    // Variable to store the size of the data read
    if (authenticateBlock(block, buffer, size)) {  // If authentication and reading are successful
      lcd.setCursor(0, 1);                         // Set the cursor to the second line of the LCD
      lcd.print("Name: ");                         // Print "Name:"
      // Print the name starting from the second character to skip the size byte
      lcd.print((char *)buffer + 1);
      Serial.print("Name: ");
      Serial.println((char *)buffer + 1);  // Print the name on the serial monitor
    }
  }

10. ä»¥ä¸‹ã®ã‚³ãƒ¼ãƒ‰ã‚’Arduino Uno R3ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã§ãã¾ã™ã€‚ãã®å¾Œã€ã‚«ãƒ¼ãƒ‰ã¾ãŸã¯ã‚¿ã‚°ã‚’RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ã‚¢ãƒ³ãƒ†ãƒŠã«è¿‘ã¥ã‘ã‚‹ã¨ã€åå‰ã¨IDãŒLCDã¨ã‚·ãƒªã‚¢ãƒ«ãƒ¢ãƒ‹ã‚¿ã®ä¸¡æ–¹ã«è¡¨ç¤ºã•ã‚Œã¾ã™ã€‚

.. code-block:: Arduino

  #include <SPI.h>                // Include the SPI library for SPI communication
  #include <MFRC522.h>            // Include the library for the RFID module
  #include <Wire.h>               // Include the Wire library for I2C communication
  #include <LiquidCrystal_I2C.h>  // Include the library for the I2C LCD

  #define RST_PIN 9  // Reset pin for the RFID module
  #define SS_PIN 10  // Slave select pin for the RFID module

  // Create an instance of the MFRC522 class to interface with the RFID module
  MFRC522 mfrc522(SS_PIN, RST_PIN);
  // Create an instance of the LiquidCrystal_I2C class for the LCD
  LiquidCrystal_I2C lcd(0x27, 16, 2);

  void setup() {
    Serial.begin(9600);                         // Start serial communication at 9600bps
    SPI.begin();                                // Initialize the SPI bus
    mfrc522.PCD_Init();                         // Initialize the RFID reader
    lcd.init();                                 // Initialize the LCD display
    lcd.backlight();                            // Turn on the backlight of the LCD
    Serial.println(F("Ready to read a card"));  // Print a message to start read
  }

  void loop() {
    // Check if a new RFID card is present and can be read
    if (!mfrc522.PICC_IsNewCardPresent() || !mfrc522.PICC_ReadCardSerial()) {
      return;  // If no new card is present, exit the loop
    }

    displayCardUID();           // Function to display the UID of the card
    readAndDisplayBlock(4);     // Function to read and display block4 of the RFID card
    mfrc522.PICC_HaltA();       // Halt the RFID card to stop reading
    mfrc522.PCD_StopCrypto1();  // Stop encryption on the communication
    delay(5000);                // Delay for 5 seconds
    lcd.clear();                // Clear the LCD display
  }

  // Function to display the UID of the RFID card
  void displayCardUID() {
    Serial.print(F("Card UID:"));                  // Print the text "Card UID:"
    lcd.clear();                                   // Clear the LCD display
    lcd.setCursor(0, 0);                           // Set the LCD cursor to the top-left
    lcd.print("UID:");                             // Print "UID:" on the LCD
    for (byte i = 0; i < mfrc522.uid.size; i++) {  // Loop through each byte of the UID
      Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
      Serial.print(mfrc522.uid.uidByte[i], HEX);  // Print UID byte in hexadecimal
      lcd.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
      lcd.print(mfrc522.uid.uidByte[i], HEX);  // Print UID byte on the LCD in hexadecimal
    }
    Serial.println();  // Print a newline on the serial monitor
  }

  // Function to authenticate and read a block from the RFID card
  bool authenticateBlock(byte block, byte *buffer, byte &size) {
    MFRC522::StatusCode status;  // Variable to hold the status of RFID operations
    MFRC522::MIFARE_Key key;     // Variable to hold the authentication key
    // Set the key to the default key known as FFFFFFFFFFFFh
    for (byte i = 0; i < 6; i++) key.keyByte[i] = 0xFF;  // Default key A for authentication

    // Authenticate the desired block with the key
    status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, block, &key, &(mfrc522.uid));
    if (status != MFRC522::STATUS_OK) {
      Serial.print(F("Authentication failed: "));
      Serial.println(mfrc522.GetStatusCodeName(status));
      return false;  // If authentication failed, return false
    }

    // Read the block after successful authentication
    status = mfrc522.MIFARE_Read(block, buffer, &size);
    if (status != MFRC522::STATUS_OK) {
      Serial.print(F("Reading failed: "));
      Serial.println(mfrc522.GetStatusCodeName(status));
      return false;  // If reading failed, return false
    }
    buffer[size - 1] = '\0';  // Ensure the string is null-terminated
    return true;              // Return true if reading is successful
  }

  // Function to read a block and display its contents
  void readAndDisplayBlock(byte block) {
    byte buffer[18];                               // Buffer to store the data read from the RFID card
    byte size = sizeof(buffer);                    // Variable to store the size of the data read
    if (authenticateBlock(block, buffer, size)) {  // If authentication and reading are successful
      lcd.setCursor(0, 1);                         // Set the cursor to the second line of the LCD
      lcd.print("Name: ");                         // Print "Name:"
      // Print the name starting from the second character to skip the size byte
      lcd.print((char *)buffer + 1);
      Serial.print("Name: ");
      Serial.println((char *)buffer + 1);  // Print the name on the serial monitor
    }
  }

**Question**

RC522-RFIDãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã—ã¦ã‚«ãƒ¼ãƒ‰ã‚„ã‚¿ã‚°ã®æƒ…å ±ã‚’èª­ã¿å–ã£ãŸã‚Šæ›¸ãè¾¼ã‚“ã ã‚Šã—ã€ãã‚Œã‚’LCDã«è¡¨ç¤ºã™ã‚‹æ–¹æ³•ã‚’ç†è§£ã—ãŸã¨ã“ã‚ã§ã€æ—¥å¸¸ä½¿ç”¨ã®ãŸã‚ã®ä¸€èˆ¬çš„ãªã‚¢ã‚¯ã‚»ã‚¹åˆ¶å¾¡ã‚·ã‚¹ãƒ†ãƒ ã‚’ã©ã®ã‚ˆã†ã«è¨­è¨ˆã—ã¾ã™ã‹ï¼Ÿè¨­è¨ˆã®ã‚¢ãƒ—ãƒ­ãƒ¼ãƒã‚’èª¬æ˜ã—ã¦ãã ã•ã„ã€‚


**Summary**

ã“ã®ãƒ¬ãƒƒã‚¹ãƒ³ã§ã¯ã€RC522ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã—ã¦RFIDæŠ€è¡“ã®åŠ›ã‚’æ´»ç”¨ã™ã‚‹æ–¹æ³•ã‚’å­¦ã³ã¾ã—ãŸã€‚åŸºæœ¬çš„ãªæ¦‚å¿µã‚’æ¢æ±‚ã—ã€å¿…è¦ãªå›è·¯ã‚’æ§‹ç¯‰ã—ã€RFIDã‚¿ã‚°ã«å€‹äººãƒ‡ãƒ¼ã‚¿ã‚’æ›¸ãè¾¼ã¿ã€èª­ã¿å–ã‚Šã€ãã®æƒ…å ±ã‚’LCDã«è¡¨ç¤ºã—ã¾ã—ãŸã€‚ã“ã®ãƒ¬ãƒƒã‚¹ãƒ³ã‚’çµ‚ãˆã‚‹é ƒã«ã¯ã€RFIDæŠ€è¡“ã‚’è‡ªåˆ†ã®ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã«çµ±åˆã—ã€ã‚·ã‚¹ãƒ†ãƒ ã‚’ã‚ˆã‚ŠåŠ¹ç‡çš„ã§ä½¿ã„ã‚„ã™ãã™ã‚‹ãŸã‚ã®çŸ¥è­˜ã‚’èº«ã«ä»˜ã‘ãŸã“ã¨ã§ã—ã‚‡ã†ã€‚
